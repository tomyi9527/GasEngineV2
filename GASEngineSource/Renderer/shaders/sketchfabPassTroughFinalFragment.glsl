#version 100

#ifdef GL_FRAGMENT_PRECISION_HIGH
   precision highp float;
#else
   precision mediump float;
#endif

varying vec2 vTexCoord0;
uniform sampler2D Texture0;
uniform sampler2D Texture1;
uniform vec4 uHalton;

#ifdef GRAIN
// https://www.shadertoy.com/view/4djSRW
// quote : "[...] it gives the same result on PCs and Macs/iOS etc. [...] The sine stuff varies a lot between GPUs."

uniform float uGrainFactor;
uniform float uTimeGrain;

// integer : 0.1031, uv (0,1 range): 443.8975
float nrand(const in vec3 uvt) {
  vec3 p3 = fract(uvt * 443.8975);
  p3 += dot(p3, p3.yzx + 19.19);
  return fract((p3.x + p3.y) * p3.z);
}

float nrandTriangle( const in vec3 n ) {
    float nrnd0 = nrand( n );

    // Convert uniform distribution into triangle-shaped distribution.
    float orig = nrnd0 * 2.0 - 1.0;
    nrnd0 = orig * inversesqrt(abs(orig));
    nrnd0 = max(-1.0, nrnd0); // Nerf the NaN generated by 0 * rsqrt(0). Thanks @FioraAeterna!
    nrnd0 = nrnd0 - sign(orig) + 0.5;

    // Result is range [-0.5,1.5] which is useful for actual dithering. convert to [0,1] for histogram.
    return (nrnd0 + 0.5) * 0.5;
    // return nrnd0;
}

#endif

#ifdef DOF_CROSS
uniform vec2 RenderSize;
uniform float uPixelRatio;
uniform vec3 uDofCross;
#endif

void main() {
    vec4 color = uHalton.z > 0.0 ? texture2D(Texture0, vTexCoord0) : texture2D(Texture1, vTexCoord0);

    #ifdef GRAIN
    float grain = nrandTriangle(vec3(vTexCoord0.xy, fract( uTimeGrain ) * 0.07));
    color.rgb = mix(color.rgb, color.rgb * (color.rgb + (1.0 - color.rgb) * 2.0 * grain), uGrainFactor);
    #endif

    #ifdef DOF_CROSS
    float ratio = RenderSize.y / RenderSize.x;
    float dist = distance(vec2(uDofCross.x, uDofCross.y * ratio), vec2(vTexCoord0.x, vTexCoord0.y * ratio));
    float widthCircle = uPixelRatio / RenderSize.x;
    color.rgb = mix(color.rgb, vec3(1.0), abs(dist - 60.0 * widthCircle) > widthCircle ? 0.0 : uDofCross.z);
    #endif

    gl_FragColor = color;
}

#define SHADER_NAME PassTroughFinal
